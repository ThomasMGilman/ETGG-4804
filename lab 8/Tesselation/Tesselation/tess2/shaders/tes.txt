//Wang hash
uint hash(uint v){
	v = 9*( (v >> 16 ) ^ (v ^ 61));
	v ^= (v>>4);
	v = v * 668265261;
	return v ^ (v>>15);
}

uint state;
float xorshift(){
	state = state ^ (state << 13);
	state = state ^ (state >> 17);
	state = state ^ (state << 15);
	return float(state & 0x7fffffff) / float(0x7fffffff);
}

void xorshiftInit(uint v){
	state = gl_PrimitiveID ^ (v<<16);
	state = hash(state);
}

//Barycentric Interpolation
vec3 interpolate(float u, float v, float w, vec3 a, vec3 b, vec3 c ){
	vec3 p0 = u * a;
	vec3 p1 = v * b;
	vec3 p2 = w * c;
	return (p0+p1+p2);
}

vec2 interpolate(float u, float v, float w, vec2 a, vec2 b, vec2 c ){
	vec2 p0 = u * a;
	vec2 p1 = v * b;
	vec2 p2 = w * c;
	return (p0+p1+p2);
}

vec3 project( vec3 v, vec3 n, vec3 p ){
	//project p to plane defined by point v and normal n
	vec4 planeEquation = vec4(n, -dot(v,n) );
	float pdistance = dot( vec4(p,1),planeEquation );
	return p - pdistance * n;
}

layout(triangles, fractional_odd_spacing) in;

in vec3 tcs_position[];
in vec3 tcs_normal[];
in vec2 tcs_texCoord[];

out vec3 tes_worldPos;
out vec2 tes_texCoord;
out vec3 tes_normal;

void main(){
	vec3 p1 = interpolate(
		gl_TessCoord[0], gl_TessCoord[1], gl_TessCoord[2],
		tcs_position[0], tcs_position[1], tcs_position[2] );

	vec3 pr1 = project( tcs_position[0], tcs_normal[0], p1 );
	vec3 pr2 = project( tcs_position[1], tcs_normal[1], p1 );
	vec3 pr3 = project( tcs_position[2], tcs_normal[2], p1 );
	vec3 p1a = interpolate(
		gl_TessCoord[0], gl_TessCoord[1], gl_TessCoord[2],
		pr1,pr2,pr3 );

	float alpha = 0.75;
	p1 = (1.0-alpha) * p1 + alpha * p1a;

	p1 = normalize(p1);
	//vec3 n1 = p1;
	vec3 n1 = interpolate(
		gl_TessCoord[0], gl_TessCoord[1], gl_TessCoord[2],
		tcs_normal[0], tcs_normal[1], tcs_normal[2] );

	tes_texCoord = interpolate(
		gl_TessCoord[0], gl_TessCoord[1], gl_TessCoord[2],
		tcs_texCoord[0], tcs_texCoord[1], tcs_texCoord[2] );

	vec4 p = vec4(p1,1.0);
	p = p * worldMatrix;
	tes_worldPos = p.xyz;
	p = p * viewMatrix;
	p = p * projMatrix;
	gl_Position = p;
	vec4 n = vec4(n1,0.0);
	n = n * worldMatrix;
	tes_normal = n.xyz;
}